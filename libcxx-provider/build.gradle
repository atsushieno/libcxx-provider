plugins {
    alias(libs.plugins.androidLibrary)
    alias(libs.plugins.kotlinAndroid)
    id 'maven-publish'
    id 'signing'
}

group "dev.atsushieno"

android {
    namespace 'dev.atsushieno.libcxx'
    compileSdk libs.versions.android.compileSdk.get().toInteger()

    defaultConfig {
        minSdk  libs.versions.android.minSdk.get().toInteger()
        targetSdk  libs.versions.android.targetSdk.get().toInteger()
        versionCode 1
        versionName libs.versions.ndk.get()
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    ndkVersion libs.versions.ndk.get()

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version libs.versions.cmake.get()
        }
    }
    buildFeatures {
        prefabPublishing = true
    }
    prefab {
        create("cxx-provider") {
            name = "cxx-provider"
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
}

tasks.register('javadocJar', Jar) {
    archiveClassifier.set('javadoc')
}
tasks.register('sourceJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = group
                artifactId = "libcxx-provider"
                version = android.defaultConfig.versionName
                from components.release

                pom {
                    name = 'libcxx-provider'
                    description = 'Utility package to ensure libc++ packaged into your aab/apk'
                    url = 'https://github.com/atsushieno/libcxx-provider'
                    scm { url.set('https://github.com/atsushieno/libcxx-provider') }
                    licenses {
                        license {
                            name = 'the MIT License'
                            url.set('https://github.com/atsushieno/libcxx-provider/blob/main/LICENSE')
                        }
                    }
                    developers {
                        developer {
                            id = 'atsushieno'
                            name = 'Atsushi Eno'
                            email = 'atsushieno@gmail.com'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                credentials {
                    username = System.getenv("OSSRH_USERNAME")
                    password = System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }

    // keep it as is. It is replaced by CI release builds
    signing {}
}
